plugins{
    id 'com.android.library'
    id 'org.mozilla.rust-android-gradle.rust-android'
}

// TODO publish to github packages

cargo {
    profile = 'release'
    module = './rust'
    libname = 'native_effects'
    targets = ["arm", "arm64"]
    prebuiltToolchains = true
    pythonCommand = "python3"
}

android {
    namespace 'ru.vtosters.shared'
    compileSdk 32
    ndkVersion '25.1.8937393'

    defaultConfig {
        minSdk 22
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"
    }
}

afterEvaluate {
    // The `cargoBuild` task isn't available until after evaluation.
    android.libraryVariants.all { variant ->
        def productFlavor = ""
        variant.productFlavors.each {
            productFlavor += "${it.name.capitalize()}"
        }
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks["generate${productFlavor}${buildType}Assets"].dependsOn(tasks["cargoBuild"])
    }
}